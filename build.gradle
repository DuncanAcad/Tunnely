plugins {
    id 'java'
    id 'idea'
}

group = 'tunnely'
version = '1.0.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url "https://www.jetbrains.com/intellij-repository/releases" }
}

dependencies {
    implementation 'com.jetbrains.intellij.java:java-gui-forms-rt:203.7148.30' // GUI Designer Library
    implementation 'com.formdev:flatlaf:3.5.1' // GUI Style Library
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': 'tunnely.client.TunnelyClient'
    }
}

tasks.register('middlemanJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'tunnely.middleman.MiddlemanServer'
    }
    from sourceSets.main.output
    archiveBaseName.set("Tunnely-Middleman")
}

tasks.register('guiJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'tunnely.client.gui.TunnelyGUI'
    }
    from sourceSets.main.output
    archiveBaseName.set("Tunnely-GUI")
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

artifacts {
    archives middlemanJar
    archives guiJar
}